物品相关操作解析

游戏中，玩家物品的管理是通过每个玩家独有的物品管理进程，结合ETS缓存表实现的。

物品相关模块有mod_goods.erl，lib_goods.erl，goods_util.erl和lib_make.erl

mod_goods.erl：物品管理进程模块，负责创建进程，进程内接收消息并调用相关操作。
lib_goods.erl：负责物品的发放和删除，物品属性修改，背包内物品更改（物品堆叠处理），掉落管理，
goods_util.erl：负责服务器启动时和用户登录时物品数据的初始化；通过指定id或者属性获取对应的物品
lib_make.erl：负责物品品质升级，强化，打洞，镶嵌，洗练的计算

游戏中的物品主要分为两种：物品原型（type_goods）和玩家所属物品（goods）。当玩家获得物品的时候，需要根据物品原型生成相应的玩家所属物品，将一些未确定的物品属性确定下来，将其和玩家id进行关联，并将物品id记录到玩家背包中。

物品的初始化分为两个步骤：
1.服务器启动时的初始化：在mod_kenel.erl中调用init_goods()，将数据库中的物品原型数据和物品属性加载进ETS缓存。
2.玩家登录时的初始化：玩家登录时调用mod_goods:start(PlayerId,CellNum,Equip)创建物品管理进程，并返回Pid，保存在#player_status.goods_pid中。

物品管理进程初始化方法：

init([PlayerId,CellNum, Equip]) ->
    ok = goods_util:init_goods_online(PlayerId),    %% 从数据库中加载玩家物品信息，放入ETS缓存
    NullCells = goods_util:get_null_cells(PlayerId, CellNum),    %% 计算背包空格
    EquipSuit = goods_util:count_equip_suit_list(PlayerId, Equip),    %% 计算套装
    GoodsStatus = #goods_status{ player_id = PlayerId, null_cells = NullCells, equip_current = [0,0,0], equip_suit_list=EquipSuit },    %% 生成物品管理进程state
    NewStatus = goods_util:get_current_equip(GoodsStatus, Equip),    %% 获取武器，坐骑，衣服列表
    {ok, NewStatus}.
进程state：

-record(goods_status, {
        player_id = 0,              % 用户ID            (REF:player-player_id)
        null_cells = [],            % 背包空格子位置
        equip_current = [0,0,0],    % 当前装备类型ID - [武器, 衣服, 坐骑]
        equip_suit = [],            % 套装属性
        ct_time = 0                 % 使用药品的冷却时间
    }).
玩家5个放放物品的位置：套装1，套装2，套装3，套装4，套装5

物品的相关操作，试着使用思维导图整理了一些，初学思维导图，感觉整理还不是很到位，当做一次练手吧：


除去基本的物品增删改查，主要还有物品的属性强化（品质升级，装备强化，洗练等），背包整理，掉落管理的操作。所有的都罗列出来有流水账的嫌疑，这里选取一些比较关键的方法进行解析：

背包内物品的叠加计算：如果是可叠加的物品，加入背包时需要和原有的同类型物品进行叠加，减少背包格子的占用

%% 更新原有的可叠加物品
%% 返回叠加现有物品后剩余的物品数量
update_overlap_goods(GoodsInfo, [Num, MaxOverlap]) ->
    case Num > 0 of
        true when GoodsInfo#goods.num =/= MaxOverlap andalso MaxOverlap > 0 ->
            {LapHasNum, RestNum} = 
                case Num + GoodsInfo#goods.num > MaxOverlap of
                     %% 总数超出可叠加数
                     true ->
                         {MaxOverlap, Num + GoodsInfo#goods.num - MaxOverlap};
                     false ->
                         {Num + GoodsInfo#goods.num, 0}
                end,
            %% 更新现有物品数量
            change_goods_num(GoodsInfo, LapHasNum);
        true ->
            RestNum = Num;
        false ->
            RestNum = 0
    end,
    [RestNum, MaxOverlap].
怪物掉落的随机系统：

%% 随机一条掉落规则，规则内包含相关的掉落物品
rand_drop_num_rule(MonId) ->
    Pattern1 = #ets_goods_drop_num{ mon_id=MonId, _='_' },
    %% 获取掉落配置
    NumRuleList = goods_util:get_goods_list_from_ets(?ETS_GOODS_DROP_NUM, Pattern1),
    case length(NumRuleList) > 0 of
        true ->
            %% 总权重
            TotalRatio1 = lists:foldl(fun(R, Sum) -> R#ets_goods_drop_num.ratio + Sum end, 0, NumRuleList),
            Ratio1 = util:rand(1, TotalRatio1),
            %% 根据随机数从权重中选取掉落规则
            F1 = fun(Rule, [Ratio, First, Result]) ->
                End = First + Rule#ets_goods_drop_num.ratio,
                case Ratio > First andalso Ratio =< End of
                    true -> [Ratio, End, Rule];
                    false -> [Ratio, End, Result]
                end
                 end,
            [Ratio1, _, NumRule] = lists:foldl(F1, [Ratio1, 0, {}], NumRuleList);
       false ->
            NumRule = {}
    end,
    NumRule.
人物装备属性的计算：

%% 人物装备属性重新计算
count_role_equip_attribute(PlayerStatus, GoodsStatus, GoodsInfo) ->
    %% 装备属性
    Effect = get_equip_attribute(PlayerStatus#player_status.id, PlayerStatus#player_status.equip, GoodsStatus#goods_status.equip_suit_list),
    %% 检查武器、衣服
    [NewGoodsStatus, _] = count_current_equip_by_info(GoodsInfo, [GoodsStatus, on]),
    %% 更新人物属性
    PlayerStatus1 = PlayerStatus#player_status{
                           equip_current = NewGoodsStatus#goods_status.equip_current,
                           equip_attribute = Effect
                    },
    %% 人物属性重新计算
    NewPlayerStatus = lib_player:count_player_attribute(PlayerStatus1),
    {ok, NewPlayerStatus, NewGoodsStatus}.

 
————————————————
版权声明：本文为CSDN博主「Hidoshisan」的原创文章，遵循 CC 4.0 BY-SA 版权协议，转载请附上原文出处链接及本声明。
原文链接：https://blog.csdn.net/s291547/article/details/88840985