场景与副本

场景和副本是玩家活动的区域，场景在服务器启动的时候由mod_scene进程创建，场景信息保存在?ETS_SCENE中，并一直存在；副本在玩家请求进入的时候会创建副本服务进程，当玩家离开的时候会撤销进程。

场景：

场景相关的模块为mod_scene.erl和lib_scene.erl，
场景的基本要素：怪物，NPC，mask（包含场景可移动坐标信息），它们在场景初始化的时候被加载。

场景初始化过程：
1.服务器启动的时候，通过mod_scene.erl启动一个场景管理进程，将所有场景id载入
2.使用load_scene(SceneId)初始化场景：

load_scene(SceneId) ->
    S = data_scene:get(SceneId),
    case S#ets_scene.type =:= 2 orelse S#ets_scene.type =:= 3 of
        true -> %% 副本、帮会的原始场景，就不加载
            ok;
        false ->
            load_npc(S#ets_scene.npc, SceneId),
            load_mon(S#ets_scene.mon, SceneId),
            ets:insert(?ETS_SCENE, S#ets_scene{id = SceneId, mon=[], npc=[], mask=[]}),
            case data_mask:get(SceneId) of
                "" -> ?ERR("场景的坐标MASK为空:~w", [SceneId]);
                Mask1 -> load_mask(Mask1, 0, 0, SceneId)
            end
    end.
场景加载过程：

1.获取对应的配置信息，根据配置信息创建NPC和怪物，将场景插入ETS_SCENE表中。
2.获取每一个NPC配置信息，插入ETS_NPC表中
3.获取每一个怪物配置信息，开启一个怪物进程（gen_fsm），并打开怪物的战斗进程，将怪物信息保存到ETS_MON中。
4.获取场景坐标mask配置信息并加载，将可移动的坐标写入ETS_CENEN_POSES中

场景管理进程state：

-record(state, {auto_sid, auto_eid}).
其中auto_sid负责生成并返回每一个副本的唯一Id

九宫格广播系统：
当玩家状态发生变化时，需要将玩家状态的改变广播给场景中的其他玩家，（例如玩家进入新场景，需要告知旧场景的人玩家已离开，告知新场景的人玩家出现），而一个完整的场景可能有很多玩家。

为了减少不必要的广播，游戏设计了九宫格系统，将广播区域范围限定在以玩家为中心的九个格子当中。
使用lib_send:send_to_area_scene/4，获取玩家当前坐标九宫格和九宫格内其余玩家，并向他们广播消息

场景对应操作：

1.玩家在场景内走路：走路协议12001，修改玩家#player_status里的x和y坐标，将玩家的新坐标通过lib_scene:move_broadcast/9广播给场景内九宫格玩家
1.获取场景用户，场景怪物，场景NPC：使用ets:match/2进行搜索ETS_ONLINE,ETS_MON,ETS_NPC
2.进入/离开场景：leave_scene(Status)和enter_scene(Status)，会通知场景九宫格内玩家
3.进入场景条件检查：check_enter(Status,SceneId)    检查进入副本的条件（等级，道具）
    进入普通场景：enter_normal_scene/2 返回新场景Id,X,Y坐标
    进入副本场景：检查玩家副本服务进程是否存在，不存在则创建，enter_normal_scene/2

玩家进入场景的判断：

%% 进入场景条件检查
check_enter(Status, Id) ->
    case get_data(Id) of
        [] ->
            {false, 0, 0, 0, <<"场景不存在!">>, 0, []};
        Scene ->
            case check_requirement(Status, Scene#ets_scene.requirement) of
                {false, Reason} -> {false, 0, 0, 0, Reason, 0, []};
                {true} ->
                    case Scene#ets_scene.type of
                        0 -> %% 普通场景
                            enter_normal_scene(Id, Scene, Status);
                        1 -> %% 普通场景
                            enter_normal_scene(Id, Scene, Status);
                        2 -> %% 副本场景
                            case is_pid(Status#player_status.pid_dungeon) andalso is_process_alive(Status#player_status.pid_dungeon) of
                                true ->
                                    enter_dungeon_scene(Scene, Status); %% 已经有副本服务进程
                                false -> %% 还没有副本服务进程
                                    Pid = case is_pid(Status#player_status.pid_team) andalso is_process_alive(Status#player_status.pid_team) of
                                        false -> %% 没有队伍，角色进程创建副本服务器
                                            mod_dungeon:start(0, self(), [{Status#player_status.id, Status#player_status.pid}]);
                                        true -> %% 有队伍，由队伍进程创建副本服务器
                                            mod_team:create_dungeon(Status#player_status.pid_team, self(), [Id, Status#player_status.id, Status#player_status.pid])
                                    end,
                                    case is_pid(Pid) of
                                        false ->
                                            {false, 0, 0, 0, <<"你不是队长不能创建副本!">>, 0, []};
                                        true ->
                                            enter_dungeon_scene(Scene, Status#player_status{pid_dungeon = Pid})
                                    end
                            end
                    end
            end
    end.
判断是副本场景还是普通场景，普通场景直接进入，副本场景检查玩家是否拥有副本服务管理进程，将进入副本的操作发送到进程内继续执行
 

副本：

副本相关模块为mod_dungeon.erl
每一个副本由一个副本进程创建并维护，当玩家请求进入副本的时候，副本服务进程才会被创建

副本服务进程的state：

-record(state, {
    team_pid = 0,    %% 副本内队伍pid
    rl = [],           %% 副本服务器所属玩家
    dsrl = [],         %% 副本场景激活条件 [[SceneId, IsOpen::boolean, Request::Atom, NPCId, RequestNum, FinishNum]]
    dsl =[]            %% 副本服务器所拥有的场景 [{SceneId, IsOpen::boolean, Tips::string}]
}).
副本基本要素：副本场景激活条件，副本场景，在副本配置中可取，当达成一定的条件后可激活副本的新场景

通过mod_dungeon:start/3创建副本进程，在lib_scene:check_enter/2中，或者mod_team:create_dungeon/3中调用

start(TeamPid, From, RoleList) ->
    {ok, Pid} = gen_server:start(?MODULE, [TeamPid, RoleList], []),
    [clear(role, Id) || {Id, _} <- RoleList],
    [mod_player:set_dungeon(Rpid, Pid) || {_, Rpid} <- RoleList, Rpid =/= From],
    Pid.
创建完副本进程后，需要修改玩家#player_status.pid_dungeon为当前副本进程pid

一些常用副本操作：

mod_dungeon:join/2，玩家主动加入副本，清除玩家原来副本，副本玩家列表加入新玩家。用于在组队中非队长的玩家加入副本。
mod_dungeon:quit/2，out/2，将玩家踢出副本，调用send_out/1，获取 副本外场景，修改玩家#player_status的坐标，场景Pid，将玩家Id从副本进程玩家id列表中删去
mod_dungeon:clear/2，清除副本进程，调用mod_scene:clear_scene/1，清除场景内容（怪物，NPC，ETS记录），并停止副本进程。
kill_npc/2，副本杀怪，更新state中的dsrl，检查如果达到新的场景开放条件，则更新dsl，激活新的副本场景

玩家进入副本的判断：

handle_call({check_enter, SceneResId}, _From, State) ->   %% 这里的SceneId是数据库的里的场景id，不是唯一id
    case lists:keyfind(SceneResId, 4, State#state.dsl) of
        false ->
            {reply, {false, <<"没有这个副本场景">>}, State};   %%没有这个副本场景
        DS ->
            case DS#ds.enable of
                false ->
                    {reply, {false, DS#ds.tip}, State};    %%还没被激活
                true ->
                    {SceneId, NewState} =
                        case DS#ds.id =/= 0 of
                            true -> {DS#ds.id, State};   %%场景已经加载过
                            false -> create_scene(SceneResId, State)
                        end,
                    {reply, {true, SceneId}, NewState}
            end
    end;
0.根据#ets_scene.type判断是否副本场景。
1.检查玩家是否有副本服务进程pid_dungeon，如果没有则通过mod_dungeon:start/3创建。
2.检查该副本服务进程场景列表里是否有该副本场景，没有则返回失败。
3.副本服务进程场景列表有该场景，判断场景是否激活。
4.检查场景是否加载过，没加载过的话调用mod_scene:copy_scene/2复制一个副本场景，取代场景列表中原来的场景。此时场景的“唯一ID”代替了场景的资源id，同样被插入到?ETS_SCENE表中

将玩家传出副本：

send_out(R) when is_record(R, ets_online) ->
    case get_dungeon_id(lib_scene:get_res_id(R#ets_online.scene)) of
        0 -> scene_not_exist;  %% 不在副本场景
        Did -> %% 将传送出副本
            DD = data_dungeon:get(Did),
            [Sid, X, Y] = DD#dungeon.out,
            Player = gen_server:call(R#ets_online.pid, 'PLAYER'),
            lib_scene:leave_scene(Player),
            Player1 = Player#player_status{pid_dungeon = none, scene = Sid, x = X, y = Y},
            gen_server:cast(R#ets_online.pid, {'SET_PLAYER', Player1}),
            {ok, BinData} = pt_12:write(12005, [Sid, X, Y, <<>>, Sid]),
            lib_send:send_one(Player1#player_status.socket, BinData)
    end.
获取副本外场景Id, 修改#player_status的场景Id和坐标并回写玩家进程，并广播玩家切换场景的消息
————————————————
版权声明：本文为CSDN博主「Hidoshisan」的原创文章，遵循 CC 4.0 BY-SA 版权协议，转载请附上原文出处链接及本声明。
原文链接：https://blog.csdn.net/s291547/article/details/88576356